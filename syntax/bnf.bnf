program ::= %w* def+ ;
w ::= comment | " " | "\t" | "\n" | "\r\n" ;
	comment ::= "#" !"\n"* "\n" ;

# test

name ::= letter ( letter | digit | "_" )* ;
	letter ::= "a"->"z" | "A"->"Z" ;
	digit ::= "0"->"9" ;
	hex   ::= "0"->"9" | "a"->"f" | "A"->"F" ;

constant ::= %"\"" frag* %"\"" ;
	frag    ::= unicode | byte | escape | !"\""+ ;
	escape  ::= %"\\" !"" ;
	byte    ::= %"\\x" ...(hex hex) ;
	unicode ::= %"\\u" ...(hex hex hex hex) ;

def ::= ...name %(w+ "::=" w*) expr %(w* ";" w*) ;

expr ::= expr_arg %w* ( ...expr_infix? %w* expr_arg %w* )* ;
	expr_arg ::= expr_prefix ( constant | expr_brackets | ...name ) ...expr_suffix? ;
	expr_prefix ::= ..."%"? ..."..."? ..."!"? ;
	expr_infix  ::= "->" | "|" ;
	expr_suffix ::= "*" | "?" | "+" ;
	expr_brackets ::= %( "(" w* ) expr %( w* ")" ) ;